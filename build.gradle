/* use:
 * Download all dependencies - \programs\gradle-3.5\bin\gradle getTestRuntimeLibs
 * Download just the export dependencies -  \programs\gradle-3.5\bin\gradle getExportLibs
 */

apply plugin: 'java'
sourceCompatibility = '11'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.destinationDirectory = file("$rootDir/deployment/run/bin")

repositories {
    mavenCentral()
    flatDir {
        dirs '/c://Program Files/Mendix/9.18.0.53394/runtime/bundles'
    }
}

configurations {
    appstoreModuleImplementation
}

test {
    useJUnitPlatform()
}

dependencies {
    // public mendix runtime jars
    implementation name:'com.mendix.public-api'
    implementation name: 'com.mendix.json'
    implementation name: 'com.mendix.logging-api'
    implementation name: 'com.mendix.m2ee-api'
    implementation name: 'javax.servlet-api.servlet'

    // test
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.1")

    // export module
    //appstoreModuleImplementation
}

clean {
    delete "userlib"
    delete "deployment/run/bin"
}


sourceSets {
    main {
        java {
            srcDirs = ['javasource']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        java {
            srcDirs = ['javasource-test']
        }
        resources {
            srcDirs = ['resources-test']
        }
    }
    appstoreModule {
            //compileClasspath += sourceSets.main.output
            //runtimeClasspath += sourceSets.main.output
    }
}

task getTestRuntimelibs(type: Copy) {
    from sourceSets.test.runtimeClasspath
    into 'userlib/'
}

task getExportLibs(type: Copy) {
    doFirst {
        delete fileTree(dir: "userlib")
    }
    from sourceSets['appstoreModule'].runtimeClasspath
    into 'userlib/'
    doLast {
        files { file('userlib').listFiles() }.each { File file ->
            println "file found: $file.name"
            def keepFilename = 'userlib/' + file.name + '.CsvServices.RequiredLib'
            ant.touch(file: keepFilename)
        }
    }
}

task exportModule(type: Exec,dependsOn: 'getExportLibs'){
    commandLine "\\Program Files\\Mendix\\9.18.0.53394\\modeler\\mxutil.exe", "create-module-package", "CsvServices.mpr", "CsvServices", "--package-dir=DIST"
    doLast {
        copy {
            from 'DIST'
            include 'CsvServices.mpk'
            into 'DIST'
            rename 'CsvServices.mpk', 'CsvServices-2.4.mpk'
        }
    }
}